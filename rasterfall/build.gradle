plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.10.2"
    id 'com.gradleup.shadow' version '8.3.5'
    id 'edu.sc.seis.launch4j' version '3.0.6'
}

group 'pt.omst'
version '2025.11.00'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'pt.omst.rasterfall.RasterFallApp'
}

// Set working directory for run task
run {
    workingDir = rootProject.projectDir
    // Pass system properties to the application
    systemProperties System.properties
}

repositories {
    mavenLocal()
    mavenCentral()
    
    flatDir {
        dirs rootProject.file('lib')
    }
    
    maven {
        url "https://omst.direct.quickconnect.to/software/maven"
    }
}

dependencies {

    implementation project(':neptus-utils')
    implementation project(':rasterlib')
    implementation project(':omst-licences')
    implementation project(':contacts')
    
    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.0'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.12'
    
    // License3j
    implementation 'com.javax0.license3j:license3j:3.2.0'
    
    // UI dependencies
    implementation name: 'flatlaf-3.5.2'
    
    // Image processing
    implementation 'com.jhlabs:filters:2.0.235-1'
    
    // Testing
    testImplementation(platform('org.junit:junit-bom:5.11.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach { 
    options.encoding = 'UTF-8' 
}

compileJava.options.encoding = 'UTF-8'

// Configure shadow jar
shadowJar {
    archiveBaseName.set('rasterfall')
    archiveClassifier.set('all')
    manifest {
        attributes 'Main-Class': 'pt.omst.rasterfall.RasterFallApp'
    }
}

// Configure Launch4j to create Windows executable
launch4j {
    mainClassName = 'pt.omst.rasterfall.RasterFallApp'
    outputDir = 'launch4j'
    outfile = 'rasterfall.exe'
    jarTask = shadowJar
    icon = "${projectDir}/../app-icons/rasterfall.ico"
    productName = 'RasterFall'
    fileDescription = 'OMST RasterFall - Sidescan Sonar Data Viewer'
    version = project.version.toString()
    textVersion = project.version.toString()
    copyright = '2025 OceanScan - Marine Systems & Technology, Lda.'
    companyName = 'OceanScan - Marine Systems & Technology'
    jreMinVersion = '21'
    bundledJrePath = 'jre'
    jvmOptions = ['-Xmx2048m']
}

// Ensure shadow jar is built before creating exe
tasks.named('createExe') {
    dependsOn shadowJar
}

// Configure publishing
publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Rasterfall Viewer'
                description = 'OMST Rasterfall viewer application for sidescan data'
                packaging = 'jar'
            }
        }
    }
}
