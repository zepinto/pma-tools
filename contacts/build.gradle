plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.10.2"
    id 'com.gradleup.shadow' version '8.3.5'
}

group 'pt.omst'
version '2025.11.00'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

application {
    mainClass = 'pt.omst.contacts.browser.TargetManager'
}

// Set working directory for run task
run {
    workingDir = rootProject.projectDir
} 

repositories {
    mavenLocal()
    mavenCentral()
    
    flatDir {
        dirs rootProject.file('lib')
    }
    
    maven {
        url "https://omst.direct.quickconnect.to/software/maven"
    }
}

dependencies {
    // Logging
    api 'org.slf4j:slf4j-api:2.0.0'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.12'
    
    // Local project dependencies
    implementation project(':neptus-utils')
    implementation project(':rasterlib')
    implementation project(':omst-licences')
    implementation project(':pulvis-api')
    
    // License3j (needed for license management)
    implementation 'com.javax0.license3j:license3j:3.2.0'
    
    // UI dependencies
    implementation name: 'flatlaf-3.5.2'
    implementation 'org.swinglabs.swingx:swingx-core:1.6.5-1'
    
    // Image scaling
    implementation 'org.imgscalr:imgscalr-lib:4.2'
    
    // CSV Reader
    implementation 'net.sourceforge.javacsv:javacsv:2.0'
    
    // Mustache templating
    implementation 'com.github.spullara.mustache.java:compiler:0.9.14'
    
    // PDF generation dependencies
    implementation 'org.jsoup:jsoup:1.18.1'
    implementation 'org.xhtmlrenderer:flying-saucer-pdf:9.9.3'
    
    // Testing
    testImplementation(platform('org.junit:junit-bom:5.11.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach { 
    options.encoding = 'UTF-8' 
}

compileJava.options.encoding = 'UTF-8'

// Configure shadow jar
shadowJar {
    archiveBaseName.set('target-manager')
    archiveClassifier.set('all')
    manifest {
        attributes 'Main-Class': 'pt.omst.contacts.browser.TargetManager'
    }
}

// Configure publishing for this library
publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Contacts'
                description = 'OMST contacts management library for marine data visualization'
                packaging = 'jar'
            }
        }
    }
}
